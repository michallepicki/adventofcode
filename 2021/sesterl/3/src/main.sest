import File
import Binary
import Char

module Main = struct

  val rec parse_input(file, acc) =
    case File.read_line(file) of
    | Error(_) -> Stdlib.List.reverse(acc)
    | Ok(line) ->
        let c_list = Stdlib.Binary.to_list(Binary.trim(line)) in
        let int_list = Stdlib.List.map(fun(c) -> Char.to_int(c) - 48 end, c_list) in
        parse_input(file, int_list :: acc)
    end

  val rec zip_with(zip_fun, left, right, acc) =
    case {left, right} of
    | {[], _} -> Stdlib.List.reverse(acc)
    | {_, []} -> Stdlib.List.reverse(acc)
    | {l :: lrest, r :: rrest} ->
      zip_with(zip_fun, lrest, rrest, zip_fun(l, r) :: acc)
    end

  val rec rating_to_decimal(digits_list, acc) =
    case digits_list of
    | [] -> acc
    | x :: rest -> rating_to_decimal(rest, (acc * 2) + x)
    end
  
  val flip_bit(x) = if x == 1 then 0 else 1

  val get_gamma_rating(input) =
    let n = Stdlib.List.length(input) in
    let first_line :: rest = input in
    let sum_rows_fun = fun(acc, row) -> zip_with(fun(a, b) -> a + b end, row, acc, []) end in
    let ones_per_position = Stdlib.List.foldl(sum_rows_fun, first_line, rest) in
    Stdlib.List.map(fun(x) -> if x > (n - x) then 1 else 0 end, ones_per_position)

  val part_1(input) =
    let gamma_rating = get_gamma_rating(input) in
    let epsilon_rating = Stdlib.List.map(flip_bit, gamma_rating) in
    print_debug(rating_to_decimal(gamma_rating, 0) * rating_to_decimal(epsilon_rating, 0))

  val rec get_rating2(input, rating_digit_fun, acc) =
    let n = Stdlib.List.length(input) in
    case input of
    | last_row :: [] -> Stdlib.List.append(Stdlib.List.reverse(acc), last_row)
    | [] :: _ -> Stdlib.List.reverse(acc)
    | _ ->
      let ones_on_position =
        Stdlib.List.foldl(fun(acc, row) ->
            let x :: _ = row in
            acc + x
          end,
          0, input) in
      let digit = rating_digit_fun(ones_on_position, n) in
      let new_input =
        Stdlib.List.filter_map(
          fun(row) ->
            let x :: rest = row in
            if x == digit then Some(rest) else None
          end,
          input) in
      get_rating2(new_input, rating_digit_fun, digit :: acc)
    end

  val get_oxygen_digit(x, n) = if x >= (n - x) then 1 else 0

  val get_co2_digit(x, n) = flip_bit(get_oxygen_digit(x, n))

  val part_2(input) =
    let oxygen = get_rating2(input, get_oxygen_digit, []) in
    let co2 = get_rating2(input, get_co2_digit, []) in
    print_debug(rating_to_decimal(oxygen, 0) * rating_to_decimal(co2, 0))

  val main() =
    let Some(file) = File.open_file("../../3") in
    let input = parse_input(file, []) in
    let _ = part_1(input) in
    part_2(input)

end
